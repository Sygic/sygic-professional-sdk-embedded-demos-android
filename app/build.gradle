apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 32

    namespace 'com.sygic.example.hello3dwiw'

    defaultConfig {
        applicationId "com.sygic.example.hello3dwiw"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            Properties localProp = new Properties()
            localProp.load(project.rootProject.file('local.properties').newDataInputStream())
            def keystoreSett = localProp.getProperty('keystore.settings', null)
            if (keystoreSett != null) {
                File signFile = file(keystoreSett)
                if (signFile.exists()) {
                    Properties properties = new Properties()
                    properties.load(signFile.newDataInputStream())

                    storeFile file(properties.getProperty('key.store', null))
                    storePassword properties.getProperty('key.store.password', null)
                    keyAlias properties.getProperty('key.alias', null)
                    keyPassword properties.getProperty('key.alias.password', null)
                }
                else {
                    logger.warn("File " + signFile + " does not exist. App will not be signed.")
                }
            }
            else {
                logger.warn("keystore.settings file not defined in local.properties. App will not be signed.")
            }
        } // release
    } // signingConfigs

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'com.sygic.fleet:embedded-with-res:22.3.4'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'
}

